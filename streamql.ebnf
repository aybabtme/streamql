/* validate it's LL(1) with http://smlweb.cpsc.ucalgary.ca/start.html */

/* Semantic */

/* CFG (more readable) */

Start -> Filters .
Filters -> Filter
         | Filter FilterSepKw Filters .

Filter -> Func
        | Func PipeSepKw Func .

Func -> Sel
      | EmitFunc .
EmitFunc -> EmitBoolFunc
          | EmitStrFunc
          | EmitNumFunc
          | EmitAnyFunc .
Sel    -> ObjectSel SubSel .
SubSel -> ObjectSel SubSel
        | ArraySel SubSel
        | epsilon.
ObjectSel -> ThisKw
           | ThisKw Member .
ArraySel -> EachKw
          | RangeEachKw
          | IndexKw .

EmitBoolFunc  -> FuncStrContains
               | FuncStrRegexp
               | AlgBoolOps .
EmitStrFunc   -> FuncStrSubstr .
EmitAnyFunc   -> FuncAnySelect .
EmitNumFunc   -> EmitIntFunc
               | EmitFloatFunc
               | AlgNumOps .
EmitIntFunc   -> FuncStrLength .
EmitFloatFunc -> epsilon .

AlgBoolOps -> FuncBoolOr
            | FuncBoolAnd
            | FuncBoolNot
            | FuncBoolXOR .

AlgNumOps -> FuncNumAdd
           | FuncNumSubtract
           | FuncNumMultiply
           | FuncNumDivide .

Member -> FieldIdentifier .
EachKw -> OpenArrayKw CloseArrayKw .
RangeEachKw -> OpenArrayKw Range CloseArrayKw .
IndexKw -> OpenArrayKw Index CloseArrayKw .
Range -> IntArg RangeSepKw IntArg .
Index -> IntArg .

FuncStrContains -> ContainsKw OpenFuncKw StrArg ArgSepKw Sel CloseFuncKw .
FuncStrRegexp   -> RegexpKw OpenFuncKw StrArg ArgSepKw Sel CloseFuncKw .
FuncStrSubstr   -> SubStrKw OpenFuncKw StrArg ArgSepKw IntArg ArgSepKw IntArg CloseFuncKw .
FuncStrLength   -> LengthKw OpenFuncKw StrArg CloseFuncKw .

FuncAnySelect      -> SelectKw OpenFuncKw BoolArg CloseFuncKw .

FuncBoolOr  -> BoolArg OrKw BoolArg .
FuncBoolAnd -> BoolArg AndKw BoolArg .
FuncBoolNot -> NotKw BoolArg .
FuncBoolXOR -> BoolArg XORKw BoolArg .

FuncNumAdd      -> NumArg AddKw NumArg .
FuncNumSubtract -> NumArg SubtractKw NumArg .
FuncNumMultiply -> NumArg MultiplyKw NumArg .
FuncNumDivide   -> NumArg DivideKw NumArg .

ContainsKw  -> contains_class .
RegexpKw    -> regexp_class .
SelectKw    -> select_class .
LengthKw    -> length_class .
SubStrKw    -> substring_class .
OpenFuncKw  -> left_parens_class .
CloseFuncKw -> right_parens_class .
ArgSepKw    -> comma_class .
OrKw        -> or_class .
AndKw       -> and_class .
NotKw       -> not_class .
XORKw       -> xor_class .
AddKw       -> add_class .
SubtractKw  -> substract_class .
MultiplyKw  -> multiply_class .
DivideKw    -> divide_class .
OpenStrKw   -> quote_class .
CloseStrKw  -> quote_class .

StrArg -> Str
        | EmitStrFunc .
BoolArg -> Bool
         | EmitBoolFunc .
NumArg -> Num
        | EmitNumFunc .
IntArg -> Int
        | EmitIntFunc .

Str    -> OpenStrKw SubStr CloseStrKw .
SubStr -> Letter SubStr
        | escape_letter_class CloseStrKw
        | epsilon .

Bool -> bool_class .

Num             -> Int SubNum .
SubNum          -> SubDecimalPart SubExponentPart
SubDecimalPart  -> DecimalKw Int
                 | epsilon .
SubExponentPart -> sci_notation_class Int
                 | epsilon .


/* LL(1) (better implementable) */


Start         -> Filters .
Filters       -> Filter Whitespace FilterChain .
FilterChain   -> FilterSepKw Whitespace Filters
               | epsilon .
Filter        -> Funcs .
Funcs         -> Func Whitespace FuncChain .
FuncChain     -> PipeSepKw Whitespace Funcs
               | epsilon .
Func          -> Sel
               | EmitFunc .
EmitFunc      -> EmitBoolFunc
               | EmitStrFunc
               | EmitNumFunc
               | EmitAnyFunc .
Sel           -> ThisKw RootObjSel .
RootObjSel    -> MemberSel SubSel
               | ArraySel SubSel
               | Whitespace epsilon .
SubSel        -> ObjSel SubSel
               | ArraySel SubSel
               | Whitespace epsilon .
ObjSel        -> ThisKw MemberSel .
MemberSel     -> Str .
ArraySel      -> OpenArrayKw ArrayOpSel .
ArrayOpSel    -> CloseArrayKw
               | Whitespace Int Whitespace ArrayOpIdx .
ArrayOpIdx    -> RangeSepKw Whitespace Int Whitespace CloseArrayKw
               | CloseArrayKw .
EmitStrFunc   -> BuiltinStrFunc .
EmitAnyFunc   -> BuiltinAnyFunc .
EmitNumFunc   -> AlgNumOps .
EmitBoolFunc  -> AlgBoolOps .

BuiltinStrFunc   -> FuncStrSubStr .
BuiltinAnyFunc   -> FuncAnySelect .
BuiltinIntFunc   -> FuncStrLength .
BuiltinFloatFunc -> epsilon .

AllEmitBoolFunc -> FuncStrContains
                 | FuncStrRegexp .

AlgBoolOps       -> BoolArg AlgBoolOpsTwoAry
                  | AlgBoolOpsUnary .
AlgBoolOpsUnary  -> FuncBoolNot .
AlgBoolOpsTwoAry -> FuncBoolOr
                  | FuncBoolAnd
                  | FuncBoolXOR
                  | epsilon .

AlgNumOps       -> NumArg AlgNumOpsTwoAry
                 | OpenPriorityKw AlgNumOps ClosePriorityKw .
AlgNumOpsTwoAry -> FuncNumAdd
                 | FuncNumSubtract
                 | FuncNumMultiply
                 | FuncNumDivide
                 | epsilon .

FuncStrContains -> ContainsKw OpenFuncKw StrArg ArgSepKw Sel CloseFuncKw .
FuncStrRegexp   -> RegexpKw OpenFuncKw StrArg ArgSepKw Sel CloseFuncKw .
FuncStrSubStr   -> SubStrKw OpenFuncKw StrArg ArgSepKw IntArg ArgSepKw IntArg CloseFuncKw .
FuncStrLength   -> LengthKw OpenFuncKw StrArg CloseFuncKw .
FuncAnySelect   -> SelectKw OpenFuncKw BoolArg CloseFuncKw .

FuncBoolOr      -> OrKw BoolArg .
FuncBoolAnd     -> AndKw BoolArg .
FuncBoolXOR     -> XORKw BoolArg .
FuncBoolNot     -> NotKw BoolArg .

FuncNumAdd      -> AddKw NumArg .
FuncNumSubtract -> SubtractKw NumArg .
FuncNumMultiply -> MultiplyKw NumArg .
FuncNumDivide   -> DivideKw NumArg .

Whitespace -> whitespace Whitespace
            | epsilon .

StrArg  -> Str
         | EmitStrFunc .
BoolArg -> Bool
         | AllEmitBoolFunc .
NumArg  -> Num
         | EmitIntFunc
         | EmitFloatFunc .
IntArg  -> Int
         | EmitIntFunc .

Str    -> OpenStrKw SubStr CloseStrKw .
SubStr -> Letter SubStr
        | Digit SubStr
        | escape_letter CloseStrKw
        | epsilon .
Letter -> letter_class .
Digit  -> digit_class .

Bool -> bool .

Num             -> Int SubNum
                 | SubNum
                 | InvKv SubNum .
SubNum          -> SubDecimalPart SubExponentPart .
SubDecimalPart  -> DecimalKw Int
                 | epsilon .
SubExponentPart -> sci_notation Int
                 | epsilon .
Int             -> SubInt
                 | InvKw SubInt .
SubInt          -> zero_digit_class
                 | non_zero_digit_class SubInt .

FilterSepKw  -> comma_class .
PipeSepKw    -> pipe_class .
ThisKw       -> dot_class .
OpenArrayKw  -> left_bracket_class .
CloseArrayKw -> right_bracket_class .
RangeSepKw   -> colon_class .

ContainsKw -> contains_class .
RegexpKw   -> regexp_class .
SelectKw   -> select_class .
LengthKw   -> length_class .
SubStrKw   -> substring_class .

OpenFuncKw      -> left_parens_class .
CloseFuncKw     -> right_parens_class .
OpenPriorityKw  -> left_parens_class .
ClosePriorityKw -> right_parens_class .
ArgSepKw        -> comma_class .
OrKw            -> or_class .
AndKw           -> and_class .
NotKw           -> not_class .
XORKw           -> xor_class .
InvKw           -> inv_class .
AddKw           -> add_class .
SubtractKw      -> substract_class .
MultiplyKw      -> multiply_class .
DivideKw        -> divide_class .
OpenStrKw       -> quote_class .
CloseStrKw      -> quote_class .
DecimalKw       -> dot_class .


/* Lexical */

Lexicon -> Whitespace
         | FieldIdentifier
         | Kws
         | Int.

/* special */

Whitespace -> whitespace_class Whitespace
            | epsilon .

/* literals */

FieldIdentifier -> InlineStr .

/* Kws */

Kws -> FilterSepKw
     | PipeSepKw
     | ThisKw
     | OpenArrayKw
     | CloseArrayKw
     | RangeSepKw
     | OpenFuncKw
     | CloseFuncKw
     | ArgSepKw
     | OpenStrKw
     | CloseStrKw .


InlineStr -> InlineLetter SubInlineStr .
SubInlineStr -> InlineLetter SubInlineStr
                 | Digit SubInlineStr
                 | epsilon .
InlineLetter -> Letter
              | escape_letter_class EscapedKw .
EscapedKw -> Kws
                | Whitespace .
Letter -> letter_class .
Digit -> digit_class .
Int -> zero_digit_class
     | non_zero_digit_class Int .

/* terminals -> regexp */

epsilon -> "" .
whitespace_class -> " " | "\n" | "\t" | "\r" .
pipe_class -> "|" .
comma_class -> "," .
dot_class -> "." .
left_bracket_class -> "[" .
right_bracket_class -> "]" .
left_parens_class -> "(" .
right_parens_class -> ")" .
colon_class -> ":" .
quote_class -> `"` .

escape_letter_class -> "\" .
letter_class -> [a-Z].
digit_class -> zero_digit_class | non_zero_digit_class.
zero_digit_class -> "0".
non_zero_digit_class -> [1-9].
bool_class -> true_class | false_class .
true_class -> "true" .
false_class -> "false" .
sci_notation_class -> "e" .

contains_class -> "contains" .
regexp_class -> "regexp" .
select_class -> "select" .
length_class -> "length" .
substring_class -> "substring" .

or_class -> "or" .
and_class -> "and" .
not_class -> "not" .
xor_class -> "xor" .

add_class -> "+" .
substract_class -> "-" .
multiply_class -> "*" .
divide_class -> "/" .

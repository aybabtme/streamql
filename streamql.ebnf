/* validate it's LL(1) with http://smlweb.cpsc.ucalgary.ca/start.html */

/* Semantic */

/* CFG (more readable) */

Start -> Filters .
Filters -> Filter
         | Filter FilterSeparatorKeyword Filters .

Filter -> Selector
        | Selector PipeSeparatorKeyword Filter .
RootSelector -> ObjectSelector Selector .
Selector -> ObjectSelector Selector
          | ArraySelector Selector
          | epsilon.
ObjectSelector -> ThisKeyword
                | ThisKeyword Member .
ArraySelector -> EachKeyword
               | RangeEachKeyword
               | IndexKeyword .

Member -> FieldIdentifier .
EachKeyword -> OpenArrayKeyword CloseArrayKeyword .
RangeEachKeyword -> OpenArrayKeyword Range CloseArrayKeyword .
IndexKeyword -> OpenArrayKeyword Index CloseArrayKeyword .
Range -> Integer RangeSeparatorKeyword Integer .
Index -> Integer .

/* LL(1) (better implementable) */

Start                 -> Filters .
Filters               -> Filter Whitespace FilterChain .
FilterChain           -> comma Whitespace Filters
                       | epsilon .
Filter                -> Selectors .
Selectors             -> RootSelector Whitespace SelectorChain .
SelectorChain         -> pipe Whitespace Selectors
                       | epsilon .
RootSelector          -> dot RootObjectSelector .
RootObjectSelector    -> MemberSelector Selector
                       | ArraySelector Selector
                       | Whitespace epsilon .
Selector              -> ObjectSelector Selector
                       | ArraySelector Selector
                       | Whitespace epsilon .
ObjectSelector        -> dot MemberSelector .
MemberSelector        -> string .
ArraySelector         -> leftBracket ArrayOpSelector .
ArrayOpSelector       -> rightBracket
                       | Whitespace integer Whitespace ArrayOpIndexor .
ArrayOpIndexor        -> colon Whitespace integer Whitespace rightBracket
                       | rightBracket .

Whitespace -> whitespace Whitespace
            | epsilon .

/* Lexical */

Lexicon -> Whitespace
         | FieldIdentifier
         | Keywords
         | Integer.

/* special */

Whitespace -> whitespace_class Whitespace
            | epsilon .

/* literals */

FieldIdentifier -> InlineString .

/* keywords */

Keywords -> FilterSeparatorKeyword
          | PipeSeparatorKeyword
          | ThisKeyword
          | OpenArrayKeyword
          | CloseArrayKeyword
          | RangeSeparatorKeyword .

FilterSeparatorKeyword -> comma_class .
PipeSeparatorKeyword -> pipe_class .
ThisKeyword -> dot_class .
OpenArrayKeyword -> left_bracket_class .
CloseArrayKeyword -> right_bracket_class .
RangeSeparatorKeyword -> colon_class .

InlineString -> InlineLetter SubInlineString .
SubInlineString -> InlineLetter SubInlineString
                 | Digit SubInlineString
                 | epsilon .
InlineLetter -> Letter
              | escape_letter_class EscapedKeyword .
EscapedKeyword -> Keywords
                | Whitespace .
Letter -> letter_class .
Digit -> digit_class .
Integer -> zero_digit_class
         | non_zero_digit_class Integer .

/* terminals -> regexp */

epsilon -> "" .
whitespace_class -> " " .
pipe_class -> "|" .
comma_class -> "," .
dot_class -> "." .
left_bracket_class -> "[" .
right_bracket_class -> "]" .
colon_class -> ":" .

escape_letter_class -> "\" .
letter_class -> [a-Z].
digit_class -> zero_digit_class | non_zero_digit_class.
zero_digit_class -> "0".
non_zero_digit_class -> [1-9].
